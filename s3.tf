data "aws_caller_identity" "current" {}

resource "aws_s3_bucket" "nba_data" {
  bucket = "${var.s3_bucket_name}-${data.aws_caller_identity.current.account_id}"
}

resource "aws_s3_bucket_server_side_encryption_configuration" "nba_data" {
  bucket = aws_s3_bucket.nba_data.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "aws:kms"
    }
    bucket_key_enabled = true
  }
}

resource "aws_s3_bucket_versioning" "nba_data" {
  bucket = aws_s3_bucket.nba_data.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_public_access_block" "nba_data" {
  bucket = aws_s3_bucket.nba_data.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_lifecycle_configuration" "nba_data" {
  bucket = aws_s3_bucket.nba_data.id

  rule {
    id     = "transition-to-glacier"
    status = "Enabled"

    filter {}

    transition {
      days          = 90
      storage_class = "GLACIER"
    }
  }
}
